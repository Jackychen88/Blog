---
title: 面向对象设计模式-策略模式
date: 2020-03-18 10:08:32
tags: [ABAP,OO]
---

[toc]
# 策略模式
定义一个抽象的策略类，然后创建一系列实际的策略实现继承于抽象策略类，这些策略实现可以互相替换，调用类直接聚合抽象策略类，这样就可以让实际策略独立于调用者的调用

![](https://raw.githubusercontent.com/Jackychen88/Picture/master/20200227154420.png)

```
@startuml
class program <<call program>> 

class ycl_mrp
abstract class ycl_strategy
class ycl_strategy_z1
class ycl_strategy_z2

hide <<call program>> circle
program --> ycl_mrp
ycl_mrp o--> ycl_strategy
ycl_strategy_z1 --|> ycl_strategy
ycl_strategy_z2 --|> ycl_strategy

class ycl_mrp{
- ycl_strategy mo_strategy
+ set_strategy( )
+ make_plan( )
}

abstract class ycl_strategy{
 call_algorithm( )
}

class ycl_strategy_z1 {
 call_algorithm( )
}

class ycl_strategy_z2 {
 call_algorithm( )
}
@enduml
```

# CLASS代码
## YCL_MRP
```
class YCL_MRP definition
  public
  final
  create public .

public section.

  methods SET_STRATEGY
    importing
      !IO_STRATEGY type ref to YCL_STRATEGY .
  methods MAKE_PLAN .
protected section.
private section.

  data MO_STRATEGY type ref to YCL_STRATEGY .
ENDCLASS.

CLASS YCL_MRP IMPLEMENTATION.
* <SIGNATURE>---------------------------------------------------------------------------------------+
* | Instance Public Method YCL_MRP->MAKE_PLAN
* +-------------------------------------------------------------------------------------------------+
* +--------------------------------------------------------------------------------------</SIGNATURE>
  method MAKE_PLAN.

    IF MO_STRATEGY IS BOUND.

      MO_STRATEGY->CALL_ALGORITHM( ).

    ENDIF.

  endmethod.


* <SIGNATURE>---------------------------------------------------------------------------------------+
* | Instance Public Method YCL_MRP->SET_STRATEGY
* +-------------------------------------------------------------------------------------------------+
* | [--->] IO_STRATEGY                    TYPE REF TO YCL_STRATEGY
* +--------------------------------------------------------------------------------------</SIGNATURE>
  METHOD SET_STRATEGY.

    IF IO_STRATEGY IS BOUND.
      MO_STRATEGY = IO_STRATEGY.
    ENDIF.

  ENDMETHOD.
ENDCLASS.
```

## YCL_STRATEGY
```
class YCL_STRATEGY definition
  public
  abstract
  create public

  global friends YCL_MRP .

public section.
protected section.

  methods CALL_ALGORITHM .
private section.
ENDCLASS.

CLASS YCL_STRATEGY IMPLEMENTATION.
* <SIGNATURE>---------------------------------------------------------------------------------------+
* | Instance Protected Method YCL_STRATEGY->CALL_ALGORITHM
* +-------------------------------------------------------------------------------------------------+
* +--------------------------------------------------------------------------------------</SIGNATURE>
  method CALL_ALGORITHM.
  endmethod.
ENDCLASS.
```

## YCL_STRATEGY_Z1
```
class YCL_STRATEGY_Z1 definition
  public
  inheriting from YCL_STRATEGY
  final
  create public .

public section.
protected section.

  methods CALL_ALGORITHM
    redefinition .
private section.
ENDCLASS.



CLASS YCL_STRATEGY_Z1 IMPLEMENTATION.


* <SIGNATURE>---------------------------------------------------------------------------------------+
* | Instance Protected Method YCL_STRATEGY_Z1->CALL_ALGORITHM
* +-------------------------------------------------------------------------------------------------+
* +--------------------------------------------------------------------------------------</SIGNATURE>
  method CALL_ALGORITHM.
    WRITE:/ '执行策略1跑MRP'.
  endmethod.
ENDCLASS.
```

## YCL_STRATEGY_Z2
```
class YCL_STRATEGY_Z2 definition
  public
  inheriting from YCL_STRATEGY
  final
  create public .

public section.
protected section.

  methods CALL_ALGORITHM
    redefinition .
private section.
ENDCLASS.

CLASS YCL_STRATEGY_Z2 IMPLEMENTATION.
* <SIGNATURE>---------------------------------------------------------------------------------------+
* | Instance Protected Method YCL_STRATEGY_Z2->CALL_ALGORITHM
* +-------------------------------------------------------------------------------------------------+
* +--------------------------------------------------------------------------------------</SIGNATURE>
  method CALL_ALGORITHM.
    WRITE:/ '执行策略2跑MRP'.
  endmethod.
ENDCLASS.
```

# 调用程序
```
*---6.策略模式
  DATA:
    GO_MRP TYPE REF TO YCL_MRP,
    GO_STRATEGY TYPE REF TO YCL_STRATEGY.
  TRY .
*---动态创建对象
    GO_MRP = NEW YCL_MRP( ).

    CREATE OBJECT GO_STRATEGY TYPE ('YCL_STRATEGY_Z1').
    GO_MRP->SET_STRATEGY( GO_STRATEGY ).
    GO_MRP->MAKE_PLAN( ).

    CREATE OBJECT GO_STRATEGY TYPE ('YCL_STRATEGY_Z2').
    GO_MRP->SET_STRATEGY( GO_STRATEGY ).
    GO_MRP->MAKE_PLAN( ).
  CATCH CX_SY_CREATE_OBJECT_ERROR INTO RX_REF.
    TXT = RX_REF->GET_TEXT( ).
    MESSAGE TXT TYPE 'I'.
  ENDTRY.
```
![](https://raw.githubusercontent.com/Jackychen88/Picture/master/20200227160501.png)