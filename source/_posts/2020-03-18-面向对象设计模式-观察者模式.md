---
title: 面向对象设计模式-观察者模式
date: 2020-03-18 10:08:32
tags: [ABAP,OO]
---

[toc]
# 观察者模式
当一个对象状态发生改变后，应该如何向其他相关的对象发送变化的通知呢？

定义对象间的一对多的依赖关系，当一个对象（被观察者）的状态发生改变时，所有依赖于它的对象（观察者）都可以得到通知并执行相关动作

观察者模式将观察者和被观察者解耦，被观察者可以按照需求增加或者删除观察者，并能够将变化及时通知所有相关观察者

![](https://raw.githubusercontent.com/Jackychen88/Picture/master/20200227145833.png)
```
@startuml
class program <<call program>> 
abstract class ycl_trigger
interface yif_listener
class ycl_trigger_mgmt
class ycl_listener_executor1
class ycl_listener_executor2

hide <<call program>> circle
program --> ycl_trigger
ycl_trigger_mgmt --|> ycl_trigger
ycl_listener_executor1 ..|> yif_listener
ycl_listener_executor2 ..|> yif_listener
ycl_trigger ..> yif_listener

abstract class ycl_trigger{
- yif_listener[] mt_listeners 
--
+ add_listener( yif_listener io_listener )
| SET HANDLER IO_LISTENER->on_status_changed FOR ME. 
+ del_listener( yif_listener io_listener ) 
| SET HANDLER IO_LISTENER->on_status_changed FOR ME ACTIVE ' '
+ post_notice( raise event status_changed)
== events ==
status_changed
}

class ycl_trigger_mgmt{
- yif_listener[] mt_listeners 
--
+ add_listener( yif_listener io_listener)
+ del_listener( yif_listener io_listener) 
+ post_notice( 实际抛出通知 )
== events ==
status_changed
}

interface yif_listener{
- on_status_changed( 注册监听ycl_trigger->status_changed)
}

class ycl_listener_executor1{
- on_status_changed( 执行监听后的逻辑 )
}

class ycl_listener_executor2{
- on_status_changed( 执行监听后的逻辑 )
}
@enduml
```

# CLASS 代码
## YCL_TRIGGER
```
class YCL_TRIGGER definition
  public
  abstract
  create public

  global friends YIF_LISTENER .

public section.

  methods ADD_LISTENER
    importing
      !IO_LISTENER type ref to YIF_LISTENER .
  methods DEL_LISTENER
    importing
      value(IO_LISTENER) type ref to YIF_LISTENER .
  methods POST_NOTICE .
protected section.
private section.

  types:
    BEGIN OF TY_LISTENER,
      LO_LISTERNER TYPE REF TO YIF_LISTENER,
    END OF TY_LISTENER .
  types:
    TTY_LISTENER TYPE STANDARD TABLE OF TY_LISTENER .

  data MT_LISTENERS type TTY_LISTENER .

  events STATUS_CHANGED .
ENDCLASS.



CLASS YCL_TRIGGER IMPLEMENTATION.


* <SIGNATURE>---------------------------------------------------------------------------------------+
* | Instance Public Method YCL_TRIGGER->ADD_LISTENER
* +-------------------------------------------------------------------------------------------------+
* | [--->] IO_LISTENER                    TYPE REF TO YIF_LISTENER
* +--------------------------------------------------------------------------------------</SIGNATURE>
  METHOD ADD_LISTENER.

    IF IO_LISTENER IS BOUND.

      READ TABLE MT_LISTENERS TRANSPORTING NO FIELDS
        WITH KEY LO_LISTERNER = IO_LISTENER.
      IF SY-SUBRC NE 0.
        APPEND INITIAL LINE TO MT_LISTENERS ASSIGNING FIELD-SYMBOL(<FS_LISTENERS>).
        <FS_LISTENERS>-LO_LISTERNER = IO_LISTENER.

        SET HANDLER IO_LISTENER->ON_STATUS_CHANGED FOR ME.

      ENDIF.
    ENDIF.

  ENDMETHOD.


* <SIGNATURE>---------------------------------------------------------------------------------------+
* | Instance Public Method YCL_TRIGGER->DEL_LISTENER
* +-------------------------------------------------------------------------------------------------+
* | [--->] IO_LISTENER                    TYPE REF TO YIF_LISTENER
* +--------------------------------------------------------------------------------------</SIGNATURE>
  method DEL_LISTENER.

    IF IO_LISTENER IS BOUND.

      DELETE MT_LISTENERS WHERE LO_LISTERNER = IO_LISTENER.

      SET HANDLER IO_LISTENER->ON_STATUS_CHANGED FOR ME ACTIVATION ' '.
    ENDIF.

  endmethod.


* <SIGNATURE>---------------------------------------------------------------------------------------+
* | Instance Public Method YCL_TRIGGER->POST_NOTICE
* +-------------------------------------------------------------------------------------------------+
* +--------------------------------------------------------------------------------------</SIGNATURE>
  method POST_NOTICE.

    RAISE EVENT STATUS_CHANGED.

  endmethod.
ENDCLASS.
```

## YIF_LISTENER
```
interface YIF_LISTENER
  public .


  data MV_NAME type STRING .

  methods ON_STATUS_CHANGED
    for event STATUS_CHANGED of YCL_TRIGGER .
endinterface.
```

## YCL_TRIGGER_MGMT
```
class YCL_TRIGGER_MGMT definition
  public
  inheriting from YCL_TRIGGER
  final
  create public .

public section.

  methods POST_NOTICE
    redefinition .
protected section.
private section.
ENDCLASS.



CLASS YCL_TRIGGER_MGMT IMPLEMENTATION.


* <SIGNATURE>---------------------------------------------------------------------------------------+
* | Instance Public Method YCL_TRIGGER_MGMT->POST_NOTICE
* +-------------------------------------------------------------------------------------------------+
* +--------------------------------------------------------------------------------------</SIGNATURE>
  METHOD POST_NOTICE.

    WRITE:/ 'START被观察者发布通知开始'.

    SUPER->POST_NOTICE( ).

    WRITE:/ 'END被观察者发布通知结束'.

  ENDMETHOD.
ENDCLASS.
```

## YCL_LISTENER_EXECUTOR1
```
class YCL_LISTENER_EXECUTOR1 definition
  public
  final
  create public .

public section.

  interfaces YIF_LISTENER .

  methods CONSTRUCTOR
    importing
      value(IV_NAME) type STRING .
protected section.
private section.
ENDCLASS.



CLASS YCL_LISTENER_EXECUTOR1 IMPLEMENTATION.


* <SIGNATURE>---------------------------------------------------------------------------------------+
* | Instance Public Method YCL_LISTENER_EXECUTOR1->CONSTRUCTOR
* +-------------------------------------------------------------------------------------------------+
* | [--->] IV_NAME                        TYPE        STRING
* +--------------------------------------------------------------------------------------</SIGNATURE>
  method CONSTRUCTOR.

    YIF_LISTENER~MV_NAME = IV_NAME.
  endmethod.


* <SIGNATURE>---------------------------------------------------------------------------------------+
* | Instance Public Method YCL_LISTENER_EXECUTOR1->YIF_LISTENER~ON_STATUS_CHANGED
* +-------------------------------------------------------------------------------------------------+
* +--------------------------------------------------------------------------------------</SIGNATURE>
  method YIF_LISTENER~ON_STATUS_CHANGED.

    WRITE:/ |监听者1{ YIF_LISTENER~MV_NAME }:接收到通知|.

  endmethod.
ENDCLASS.
```

## YCL_LISTENER_EXECUTOR2
```
class YCL_LISTENER_EXECUTOR2 definition
  public
  final
  create public .

public section.

  interfaces YIF_LISTENER .

  methods CONSTRUCTOR
    importing
      value(IV_NAME) type STRING .
protected section.
private section.
ENDCLASS.



CLASS YCL_LISTENER_EXECUTOR2 IMPLEMENTATION.


* <SIGNATURE>---------------------------------------------------------------------------------------+
* | Instance Public Method YCL_LISTENER_EXECUTOR2->CONSTRUCTOR
* +-------------------------------------------------------------------------------------------------+
* | [--->] IV_NAME                        TYPE        STRING
* +--------------------------------------------------------------------------------------</SIGNATURE>
  method CONSTRUCTOR.

    YIF_LISTENER~MV_NAME = IV_NAME.

  endmethod.


* <SIGNATURE>---------------------------------------------------------------------------------------+
* | Instance Public Method YCL_LISTENER_EXECUTOR2->YIF_LISTENER~ON_STATUS_CHANGED
* +-------------------------------------------------------------------------------------------------+
* +--------------------------------------------------------------------------------------</SIGNATURE>
  method YIF_LISTENER~ON_STATUS_CHANGED.

    WRITE:/ |监听者2{ YIF_LISTENER~MV_NAME }:接收到通知|.

  endmethod.
ENDCLASS.
```

# 调用程序
```
*---5.观察者模式
  DATA:
    GO_TRIGGER TYPE REF TO YCL_TRIGGER,
    GO_LISTENER_EXECUTOR1 TYPE REF TO YIF_LISTENER,
    GO_LISTENER_EXECUTOR2 TYPE REF TO YIF_LISTENER.

  GO_TRIGGER = NEW YCL_TRIGGER_MGMT( ).

  GO_LISTENER_EXECUTOR1 = NEW YCL_LISTENER_EXECUTOR1( 'A1' ).
  GO_TRIGGER->ADD_LISTENER( GO_LISTENER_EXECUTOR1 ).

  GO_LISTENER_EXECUTOR2 = NEW YCL_LISTENER_EXECUTOR2( 'B1' ).
  GO_TRIGGER->ADD_LISTENER( GO_LISTENER_EXECUTOR2 ).

  GO_LISTENER_EXECUTOR2 = NEW YCL_LISTENER_EXECUTOR2( 'B2' ).
  GO_TRIGGER->ADD_LISTENER( GO_LISTENER_EXECUTOR2 ).

  GO_LISTENER_EXECUTOR1 = NEW YCL_LISTENER_EXECUTOR1( 'A3' ).
  GO_TRIGGER->ADD_LISTENER( GO_LISTENER_EXECUTOR1 ).

  GO_TRIGGER->DEL_LISTENER( GO_LISTENER_EXECUTOR1 ).
  GO_TRIGGER->DEL_LISTENER( GO_LISTENER_EXECUTOR2 ).

  GO_TRIGGER->POST_NOTICE( ).
```
![](https://raw.githubusercontent.com/Jackychen88/Picture/master/20200227144600.png)

