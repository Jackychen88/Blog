---
title: 面向对象设计模式-适配器模式
date: 2020-03-18 10:08:32
tags: [ABAP,OO]
---

[toc]
# 适配器模式
在程序开发中，经常会有新开发的程序调用遗留程序的情况，而原有程序汪汪会因为设计的接口不符合新程序的调用规则，而不能直接使用。
这是如果修改原有代码，有可能要重测试，为了使设计符合“开放封闭原则”和“迪米特法则”，我们需要对原有的代码调用做适当的调整和变换，把原有的调用方法转换为所需程序的调用方法，这种模式称为适配器模式。

如类图所示，接口yif_target为目标接口，定义了调用程序所需要的调用方法和参数，他可以是一个接口或者抽象类。

类ycl_trget_adapter是适配器类，实现了yif_target的接口，并且同时继承了被适配的老程序ycl_source_old类，左手关联接口，右手关联老程序，起到一个中转适配的作用。如果是类，那么适配器中要集成老类的method，并用super调用重写他，才能在新接口的method中无缝使用

被适配的功能可以是类，也可以是功能，程序，或者是一个webservice,他最终会被适配器中的方法调用。

![](https://raw.githubusercontent.com/Jackychen88/Picture/master/20200225170218.png)
```
@startuml
class program <<call program>> 
interface yif_target
class ycl_target_adapter
class ycl_source_old

hide <<call program>> circle
note "create_doc( )调用me->create_doc_old( )" as n1
note "create_doc_old( )调用supper->create_doc_old( )" as n2

program --> yif_target
ycl_target_adapter ..|> yif_target
ycl_target_adapter --|> ycl_source_old
ycl_target_adapter .. n1
ycl_target_adapter .. n2

interface yif_target{
+ create_doc( )
}

class ycl_target_adapter{
+ create_doc( )
+ create_doc_old( )
}

class ycl_source_old{
+ create_doc_old( )
}
@enduml
```

# CLASS 代码
## YIF_TARGET
```
interface YIF_TARGET
  public .
  methods CREATE_DOC .
endinterface.
```

## YCL_TARGET_ADAPTER
```
class YCL_TARGET_ADAPTER definition
  public
  inheriting from YCL_SOURCE_OLD
  final
  create public .

public section.

  interfaces YIF_TARGET .
protected section.

  methods CREATE_DOC_OLD
    redefinition .
private section.
ENDCLASS.

CLASS YCL_TARGET_ADAPTER IMPLEMENTATION.

* <SIGNATURE>---------------------------------------------------------------------------------------+
* | Instance Protected Method YCL_TARGET_ADAPTER->CREATE_DOC_OLD
* +-------------------------------------------------------------------------------------------------+
* +--------------------------------------------------------------------------------------</SIGNATURE>
  METHOD CREATE_DOC_OLD.

    WRITE:/ '适配旧的逻辑开始'.
*---调用老代码
    SUPER->CREATE_DOC_OLD( ).

    WRITE:/ '适配旧的逻辑结束'.
  ENDMETHOD.


* <SIGNATURE>---------------------------------------------------------------------------------------+
* | Instance Public Method YCL_TARGET_ADAPTER->YIF_TARGET~CREATE_DOC
* +-------------------------------------------------------------------------------------------------+
* +--------------------------------------------------------------------------------------</SIGNATURE>
  method YIF_TARGET~CREATE_DOC.

     ME->CREATE_DOC_OLD( ).

  endmethod.
ENDCLASS.
```

## YCL_SOURCE_OLD
```
class YCL_SOURCE_OLD definition
  public
  create private

  global friends YCL_TARGET_ADAPTER .

public section.
protected section.

  methods CREATE_DOC_OLD .
private section.
ENDCLASS.

CLASS YCL_SOURCE_OLD IMPLEMENTATION.
* <SIGNATURE>---------------------------------------------------------------------------------------+
* | Instance Protected Method YCL_SOURCE_OLD->CREATE_DOC_OLD
* +-------------------------------------------------------------------------------------------------+
* +--------------------------------------------------------------------------------------</SIGNATURE>
  method CREATE_DOC_OLD.

*--创建旧单据
    WRITE:/ 'Create old doc'.

  endmethod.
ENDCLASS.
```

# 调用程序
```
*---3.适配器模式
  DATA:
    GO_TARGET TYPE REF TO YIF_TARGET.

  GO_TARGET = NEW YCL_TARGET_ADAPTER( ).

  GO_TARGET->CREATE_DOC( ).
```
![](https://raw.githubusercontent.com/Jackychen88/Picture/master/20200225171548.png)