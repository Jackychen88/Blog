---
title: 递归查找数值的组合方式
date: 2019-06-05 11:18:16
tags: [ABAP,递归，工具]
---

# 需求
近段时间一致在调整财务报表和调查金额差异，经常会有一个场景：用户提供的总金额和我导出的汇总金额有差异，我希望能通过这个差异值定位到对应明细的具体行项上去（当然是凑数据），当数据量少时还能人为凑一下，数据量一大，脑子就不够用，而且排列组合的可能性很大。
+ 举个例子,以下金额中需要找到差异189198，当然可以生凑几种可能譬如15786+173412，也就是行3和行6漏了导致差异，但数据一多，就不那么好算了，而且可能的组合也很容易遗漏
    | 行项 | 金额    |
    | ---- | ------- |
    | 1    | 60      |
    | 2    | 4567    |
    | 3    | 15786   |
    | 4    | 94258   |
    | 5    | 142,031 |
    | 6    | 173412  |
    | 7    | 176732  |
    | 8    | 219000  |
    | 9    | 256539  |

# 解决方法
通过递归遍历让系统暴力的排列组合，凑出一致的值，源码如下
![](https://raw.githubusercontent.com/Jackychen88/Picture/master/20190605111139.png)
![](https://raw.githubusercontent.com/Jackychen88/Picture/master/20190605111210.png)

# 源码
```
*&---------------------------------------------------------------------*
*& Report  YTEST_JK114
*&
*&---------------------------------------------------------------------*
*&
*&
*&---------------------------------------------------------------------*
REPORT YTEST_JK114.

TYPES:
  BEGIN OF TY_TABLE,
    QTY TYPE P DECIMALS 2,
  END OF TY_TABLE,
  TTY_TABLE TYPE STANDARD TABLE OF TY_TABLE.
****************************************************************
*CLASS 定义
****************************************************************
CLASS LCL_CX_CHECK_ERR DEFINITION INHERITING FROM CX_STATIC_CHECK.
  PUBLIC SECTION.
    METHODS: CONSTRUCTOR.
ENDCLASS.
CLASS LCL_SPOT DEFINITION.
  PUBLIC SECTION.
    CLASS-DATA:
      MT_RECORD TYPE TTY_TABLE.

    DATA:
      MV_INDEX TYPE I,
      MV_START TYPE I,
      MV_VALUE TYPE P DECIMALS 2.

    METHODS:
      CONSTRUCTOR IMPORTING IT_RECORD TYPE TTY_TABLE,
      CHANG IMPORTING IV_INDEX TYPE I RAISING LCL_CX_CHECK_ERR,
      CHANG_NEXT RAISING LCL_CX_CHECK_ERR,
      SET_START IMPORTING IV_INDEX_START TYPE I.

ENDCLASS.

CLASS LCL_SPOTS DEFINITION.
  PUBLIC SECTION.
    TYPES:
      BEGIN OF TY_SPOT,
        LO_SPOT TYPE REF TO LCL_SPOT,
      END OF TY_SPOT,
      TTY_SPOT TYPE STANDARD TABLE OF TY_SPOT.
    DATA:
      MT_SPOT       TYPE TTY_SPOT,
      MV_FIND_VALUE TYPE P DECIMALS 2.

    METHODS:
      CONSTRUCTOR IMPORTING IV_FIND TYPE ANY,
      ADD_SPOT IMPORTING IO_SPOT TYPE REF TO LCL_SPOT,
      ACTIVE_SPOT IMPORTING IV_INDEX TYPE I CHANGING OO_SPOT TYPE REF TO LCL_SPOT RAISING LCL_CX_CHECK_ERR,
      REVIEW  RETURNING VALUE(OV_FLG_EQ) TYPE CHAR1,
      WRITE_RESULT,
      REST_INDEX.
ENDCLASS.

****************************************************************
*CLASS 实现
****************************************************************
CLASS LCL_SPOT IMPLEMENTATION.
  METHOD CONSTRUCTOR.

    MT_RECORD = IT_RECORD.

  ENDMETHOD.

  METHOD CHANG.

    READ TABLE MT_RECORD INTO DATA(LS_RECORD) INDEX IV_INDEX.
    IF SY-SUBRC = 0.
      ME->MV_INDEX = IV_INDEX.
      ME->MV_VALUE = LS_RECORD-QTY.
    ELSE.
      CLEAR ME->MV_INDEX.
      DATA(R_CX) = NEW LCL_CX_CHECK_ERR( ).
      RAISE EXCEPTION R_CX.
    ENDIF.

  ENDMETHOD.

  METHOD CHANG_NEXT.
    IF MV_START > MV_INDEX + 1.
      ME->MV_INDEX = MV_START.
    ELSE.
      ME->MV_INDEX = ME->MV_INDEX + 1.
    ENDIF.
    TRY .
        ME->CHANG( ME->MV_INDEX ).
      CATCH LCL_CX_CHECK_ERR INTO DATA(LO_EXC).
        RAISE EXCEPTION LO_EXC.
    ENDTRY.

  ENDMETHOD.

  METHOD SET_START.
    MV_START = IV_INDEX_START.
  ENDMETHOD.
ENDCLASS.

CLASS LCL_SPOTS IMPLEMENTATION.
  METHOD CONSTRUCTOR.
    ME->MV_FIND_VALUE = IV_FIND.

  ENDMETHOD.
  METHOD ADD_SPOT.
    APPEND INITIAL LINE TO MT_SPOT ASSIGNING FIELD-SYMBOL(<FS_SPOT>).
    <FS_SPOT>-LO_SPOT = IO_SPOT.
  ENDMETHOD.

  METHOD ACTIVE_SPOT.
    READ TABLE MT_SPOT ASSIGNING FIELD-SYMBOL(<FS_SPOT_A>) INDEX IV_INDEX.
    IF SY-SUBRC = 0.
      OO_SPOT = <FS_SPOT_A>-LO_SPOT.
    ELSE.
      DATA(R_CX) = NEW LCL_CX_CHECK_ERR( ).
      RAISE EXCEPTION R_CX.
    ENDIF.
  ENDMETHOD.

  METHOD REVIEW.
    DATA:
      LV_VALUE_SUM TYPE P DECIMALS 2.
    LOOP AT MT_SPOT INTO DATA(LS_SPOT).
      LV_VALUE_SUM = LV_VALUE_SUM + LS_SPOT-LO_SPOT->MV_VALUE.
    ENDLOOP.
    IF LV_VALUE_SUM = ME->MV_FIND_VALUE.
      OV_FLG_EQ = 'X'.
    ENDIF.

  ENDMETHOD.

  METHOD WRITE_RESULT.
    ULINE.
    LOOP AT MT_SPOT INTO DATA(LS_SPOT).

      WRITE:/0(5)  LS_SPOT-LO_SPOT->MV_INDEX,
             8(15) LS_SPOT-LO_SPOT->MV_VALUE.
    ENDLOOP.

  ENDMETHOD.

  METHOD REST_INDEX.
    LOOP AT MT_SPOT ASSIGNING FIELD-SYMBOL(<FS_SPOT>).
      CLEAR:
      <FS_SPOT>-LO_SPOT->MV_START,
      <FS_SPOT>-LO_SPOT->MV_INDEX,
      <FS_SPOT>-LO_SPOT->MV_VALUE.
    ENDLOOP.

  ENDMETHOD.
ENDCLASS.

CLASS LCL_CX_CHECK_ERR IMPLEMENTATION.
  METHOD CONSTRUCTOR.
    SUPER->CONSTRUCTOR( ).
  ENDMETHOD.

ENDCLASS.

SELECTION-SCREEN COMMENT /1(50) comm1.
PARAMETERS:
  P_FIND TYPE P DECIMALS 2 OBLIGATORY.

AT SELECTION-SCREEN OUTPUT.
  comm1 = '先将待搜索的数据Ctrl+V,再输入要搜索的值'.

START-OF-SELECTION.

  DATA:
    GT_TABLE    TYPE TTY_TABLE,
    GV_FIND_QTY TYPE P DECIMALS 2.
*
*  GT_TABLE = VALUE #(
*  ( QTY = '60' )
*  ( QTY = '4567.00' )
*  ( QTY = '15786.00' )
*  ( QTY = '94258.00' )
*  ( QTY = '142031.00' )
*  ( QTY = '173412.00' )
*  ( QTY = '176732.00' )
*  ( QTY = '219000.00' )
*  ( QTY = '256539.00' )
*  ( QTY = '480000.00' )
*  ( QTY = '509029.00' )
*  ( QTY = '635948.00' )
*  ( QTY = '648000.00' )
*  ( QTY = '712282.00' )
*  ( QTY = '807975.00' )
*  ( QTY = '840000.00' )
*  ( QTY = '864977.00' )
*  ( QTY = '886000.00' )
*  ( QTY = '946382.00' )
*  ( QTY = '973933.00' )
*  ( QTY = '984186.00' )
*  ( QTY = '1084195.00' )
*  ( QTY = '1110000.00' )
*  ( QTY = '1200000.00' )
*  ( QTY = '1233000.00' )
*  ( QTY = '1233735.00' )
*  ( QTY = '1255344.00' )
*  ( QTY = '1260000.00' )
*  ( QTY = '1262966.00' )
*  ( QTY = '1275852.00' )
*  ( QTY = '1276273.00' )
*  ( QTY = '1351000.00' )
*  ( QTY = '1385374.00' )
*  ( QTY = '1391320.00' )
*  ( QTY = '1423015.00' )
*  ( QTY = '1477120.00' )
*  ( QTY = '1503047.00' )
*  ( QTY = '1546855.00' )
*  ( QTY = '1600657.00' )
*  ( QTY = '1660722.00' )
*  ( QTY = '1971881.00' )
*  ( QTY = '2004000.00' )
*  ( QTY = '2006651.00' )
*  ( QTY = '2315000.00' )
*  ( QTY = '2318935.00' )
*  ( QTY = '2448818.00' )
*  ( QTY = '2533761.00' )
*  ( QTY = '2543906.00' )
*  ( QTY = '2656225.00' )
*  ( QTY = '2689511.00' )
*  ( QTY = '2694999.00' )
*  ( QTY = '2732194.00' )
*  ( QTY = '2883310.00' )
*  ( QTY = '3098557.00' )
*  ( QTY = '3288958.00' )
*  ( QTY = '3799806.00' )
*  ( QTY = '4282019.00' )
*  ( QTY = '4842045.00' )
*  ( QTY = '5280762.00' )
*  ( QTY = '5937580.00' )
*  ( QTY = '7148457.00' )
*  ( QTY = '8435181.00' )
*  ( QTY = '12369852.00' )
*  ).

  GV_FIND_QTY = P_FIND.
  PERFORM CLIPTOITAB TABLES GT_TABLE[].

  IF GT_TABLE IS INITIAL.
    MESSAGE S000(00) WITH '剪贴板无数据' DISPLAY LIKE 'E'.
    LEAVE LIST-PROCESSING.
  ENDIF.

*---执行检查
  PERFORM FRM_CHECK.


*&---------------------------------------------------------------------*
*&      Form  FRM_CHECK
*&---------------------------------------------------------------------*
*  执行检查
*----------------------------------------------------------------------*
FORM FRM_CHECK .
  DATA:
    LV_FLG_EQ      TYPE CHAR1,
    LV_INDEX       TYPE I,
    LR_SPOT        TYPE REF TO LCL_SPOT,
    LR_SPOT_ACTIVE TYPE REF TO LCL_SPOT,
    LR_SPOTS       TYPE REF TO LCL_SPOTS.

*  SORT GT_TABLE BY QTY DESCENDING.
*  GV_FIND_QTY = '19084708.00'.
  LR_SPOTS = NEW LCL_SPOTS( IV_FIND = GV_FIND_QTY ).
  LR_SPOT = NEW LCL_SPOT( IT_RECORD = GT_TABLE ) .

  DATA(LV_LINES) = LINES( GT_TABLE ).

  DO LV_LINES TIMES.

    CALL FUNCTION 'SAPGUI_PROGRESS_INDICATOR'
      EXPORTING
        TEXT = |已探索了{ SY-INDEX }层|.

    LR_SPOT = NEW LCL_SPOT( IT_RECORD = GT_TABLE ) .
    LR_SPOTS->ADD_SPOT( LR_SPOT ).

    LV_INDEX = 1.
    PERFORM FRM_RUN USING 1
                          1
                 CHANGING LR_SPOTS
                          LV_FLG_EQ.

    LR_SPOTS->REST_INDEX( ).

  ENDDO.
ENDFORM.
*&---------------------------------------------------------------------*
*&      Form  FRM_RUN
*&---------------------------------------------------------------------*
*       递归
*----------------------------------------------------------------------*
FORM FRM_RUN USING IV_INDEX TYPE I
                    IV_INDEX_START TYPE I
           CHANGING OR_SPOTS TYPE REF TO LCL_SPOTS
                    OV_FLG_EQ TYPE CHAR1.
  DATA:
    LV_FLG_SKIP      TYPE CHAR1,
    LV_INDEX         TYPE I,
    LV_INDEX_NEXT    TYPE I,
    LV_INDEX_ST_NEXT TYPE I,
    LR_SPOT_ACTIVE   TYPE REF TO LCL_SPOT.

  DO.
    IF OV_FLG_EQ EQ 'X'.
      EXIT.
    ENDIF.
    TRY .
        OR_SPOTS->ACTIVE_SPOT( EXPORTING IV_INDEX = IV_INDEX CHANGING OO_SPOT = LR_SPOT_ACTIVE ).

        IF LR_SPOT_ACTIVE IS NOT INITIAL.
          LR_SPOT_ACTIVE->SET_START( IV_INDEX_START ).
          LR_SPOT_ACTIVE->CHANG_NEXT( ).

          LV_INDEX_NEXT = IV_INDEX + 1.
          LV_INDEX_ST_NEXT = LR_SPOT_ACTIVE->MV_INDEX + 1.
          PERFORM FRM_RUN USING LV_INDEX_NEXT
                                LV_INDEX_ST_NEXT
                       CHANGING OR_SPOTS
                                OV_FLG_EQ.
        ENDIF.
      CATCH LCL_CX_CHECK_ERR.
        LV_FLG_SKIP = 'X'.
    ENDTRY.

    IF LV_FLG_SKIP = 'X'.

      OV_FLG_EQ = OR_SPOTS->REVIEW( ).
      IF OV_FLG_EQ  = 'X'.
        OR_SPOTS->WRITE_RESULT( ).
        EXIT.
      ENDIF.

      EXIT.
    ENDIF.

  ENDDO.

ENDFORM.

*----------------------------------------------------------------------*
*  剪贴板数据到内表，根据制表符分割
*  如果有数据类型错误，比如内表是数值型但是剪贴板里面是字母，
*  则弹出W消息，并忽略这条数据
*----------------------------------------------------------------------*
FORM CLIPTOITAB TABLES ITAB.
  DATA: HTAB TYPE C VALUE CL_ABAP_CHAR_UTILITIES=>HORIZONTAL_TAB .
  DATA: LT_CLIP TYPE TABLE OF CHAR2048 WITH HEADER LINE,
        LT_FLD  TYPE TABLE OF CHAR2048 WITH HEADER LINE.
  DATA: CXROOT TYPE REF TO CX_ROOT,
        EXCMSG TYPE        STRING.
  FIELD-SYMBOLS:<FS_FLD> ,
                <FS_TAB> .

  ASSIGN ITAB TO <FS_TAB> .

  CALL METHOD CL_GUI_FRONTEND_SERVICES=>CLIPBOARD_IMPORT
    IMPORTING
      DATA                 = LT_CLIP[]
    EXCEPTIONS
      CNTL_ERROR           = 1
      ERROR_NO_GUI         = 2
      NOT_SUPPORTED_BY_GUI = 3
      OTHERS               = 4.
  IF SY-SUBRC <> 0.
    MESSAGE ID SY-MSGID TYPE SY-MSGTY NUMBER SY-MSGNO
      WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4  .
  ELSE.
    CALL METHOD CL_GUI_CFW=>FLUSH .
    LOOP AT LT_CLIP.
      CLEAR LT_FLD[].
      SPLIT LT_CLIP AT HTAB INTO TABLE LT_FLD.
      LOOP AT LT_FLD.
        ASSIGN COMPONENT SY-TABIX OF STRUCTURE <FS_TAB> TO <FS_FLD>.
        CHECK SY-SUBRC = 0.
        TRY .
            LT_FLD = TRANSLATE( VAL = LT_FLD FROM = |\r| TO = '' ).
            LT_FLD = TRANSLATE( VAL = LT_FLD FROM = |\n| TO = '' ).
            LT_FLD = TRANSLATE( VAL = LT_FLD FROM = |"| TO = '' ).

            CALL FUNCTION 'OIU_ME_CHAR_TO_NUMBER'
              EXPORTING
                I_CHAR               = LT_FLD
              IMPORTING
*               E_FLOAT              =
                E_PACKED             = <FS_FLD>
              EXCEPTIONS
               INVALID_NUMBER       = 1
               OTHERS               = 2
                      .
*            <FS_FLD> = LT_FLD.
            IF SY-SUBRC <> 0.
* Implement suitable error handling here
            ENDIF.

          CATCH  CX_ROOT INTO CXROOT.
            EXCMSG = CXROOT->GET_TEXT( ).
        ENDTRY.
      ENDLOOP.
      APPEND ITAB .
      CLEAR ITAB.
    ENDLOOP.
    IF EXCMSG IS NOT INITIAL.
      MESSAGE W000(OO) WITH '数据转换有错误发生，已经忽略'.
    ENDIF.
  ENDIF.
ENDFORM.
```