---
title: 面向对象设计模式-简单工厂模式
date: 2020-03-18 10:08:32
tags: [ABAP,OO]
---

[toc]

# 简单工厂模式
定义一个工厂类，它可以根据参数的不同返回不同类的实例，被创建的实例通常都具有共同的父类

简单工厂模式包含以下角色和职责：
+ 工厂（Creator）：简单工厂模式的核心，它负责实现创建所有实例的内部逻辑。工厂类的创建产品类的方法可以被外界直接调用，创建所需的产品对象。
+ 抽象产品（Product）：简单工厂模式所创建的所有对象的父类，它负责描述所有实例所共有的公共接口。
+ 具体产品（Concrete Product）：是简单工厂模式的创建目标，所有创建的对象都是充当这个角色的某个具体类的实例。

![](https://raw.githubusercontent.com/Jackychen88/Picture/master/20200227162944.png)

[在线UML类图](http://www.plantuml.com/plantuml/uml)
```
@startuml
class program <<call program>> 
hide <<call program>> circle

class ycl_operationfactory
abstract class ycl_operation 
class ycl_operation_add
class ycl_operation_sub

program --> ycl_operationfactory
ycl_operation<..ycl_operationfactory
ycl_operation<|--ycl_operation_add
ycl_operation<|--ycl_operation_sub

class ycl_operationfactory{
+ {static} create_instance( ycl_operation ro_operation)
}

abstract class ycl_operation{
-mv_number_a i
-mv_number_b i

+ set_number_a( iv_number_a i )
+ set_number_b( iv_number_b i )
+ get_number_a( rv_number_a i )
+ get_number_b( rv_number_b i )
+ get_result( rv_result i )
}

class ycl_operation_add{
+ get_result( rv_result i )
}

class ycl_operation_sub{
+ get_result( rv_result i )
}
@enduml
```

# CLASS代码
## YCL_OPERATIONFACTORY 工厂类
```
class YCL_OPERATIONFACTORY definition
  public
  final
  create private .

public section.

  class-methods CREATE_INSTANCE
    importing
      value(IV_OPERATE) type STRING
    returning
      value(RO_OPERATION) type ref to YCL_OPERATION .
protected section.
private section.
ENDCLASS.



CLASS YCL_OPERATIONFACTORY IMPLEMENTATION.


* <SIGNATURE>---------------------------------------------------------------------------------------+
* | Static Public Method YCL_OPERATIONFACTORY=>CREATE_INSTANCE
* +-------------------------------------------------------------------------------------------------+
* | [--->] IV_OPERATE                     TYPE        STRING
* | [<-()] RO_OPERATION                   TYPE REF TO YCL_OPERATION
* +--------------------------------------------------------------------------------------</SIGNATURE>
  method CREATE_INSTANCE.

    CASE IV_OPERATE.
      WHEN '+'.
        RO_OPERATION = new YCL_OPERATION_ADD( ).
      WHEN '-'.
        RO_OPERATION = new YCL_OPERATION_SUB( ).
      WHEN OTHERS.
    ENDCASE.

  endmethod.
ENDCLASS.
```
## YCL_OPERATION 产品抽象类
```
class YCL_OPERATION definition
  public
  abstract
  create public .

public section.

  methods SET_NUMBER_A
    importing
      value(IV_NUMBER_A) type I .
  methods SET_NUMBER_B
    importing
      value(IV_NUMBER_B) type I .
  methods GET_NUMBER_A
    returning
      value(RV_NUMBER_A) type I .
  methods GET_NUMBER_B
    returning
      value(RV_NUMBER_B) type I .
  methods GET_RESULT
    returning
      value(RV_RESULT) type I .
protected section.

  data MV_NUMBER_A type I .
  data MV_NUMBER_B type I .
private section.
ENDCLASS.



CLASS YCL_OPERATION IMPLEMENTATION.


* <SIGNATURE>---------------------------------------------------------------------------------------+
* | Instance Public Method YCL_OPERATION->GET_NUMBER_A
* +-------------------------------------------------------------------------------------------------+
* | [<-()] RV_NUMBER_A                    TYPE        I
* +--------------------------------------------------------------------------------------</SIGNATURE>
  method GET_NUMBER_A.
    RV_NUMBER_A = MV_NUMBER_A.
  endmethod.


* <SIGNATURE>---------------------------------------------------------------------------------------+
* | Instance Public Method YCL_OPERATION->GET_NUMBER_B
* +-------------------------------------------------------------------------------------------------+
* | [<-()] RV_NUMBER_B                    TYPE        I
* +--------------------------------------------------------------------------------------</SIGNATURE>
  method GET_NUMBER_B.
    RV_NUMBER_B = MV_NUMBER_B.
  endmethod.


* <SIGNATURE>---------------------------------------------------------------------------------------+
* | Instance Public Method YCL_OPERATION->GET_RESULT
* +-------------------------------------------------------------------------------------------------+
* | [<-()] RV_RESULT                      TYPE        I
* +--------------------------------------------------------------------------------------</SIGNATURE>
  method GET_RESULT.
  endmethod.


* <SIGNATURE>---------------------------------------------------------------------------------------+
* | Instance Public Method YCL_OPERATION->SET_NUMBER_A
* +-------------------------------------------------------------------------------------------------+
* | [--->] IV_NUMBER_A                    TYPE        I
* +--------------------------------------------------------------------------------------</SIGNATURE>
  method SET_NUMBER_A.

    MV_NUMBER_A = IV_NUMBER_A.

  endmethod.


* <SIGNATURE>---------------------------------------------------------------------------------------+
* | Instance Public Method YCL_OPERATION->SET_NUMBER_B
* +-------------------------------------------------------------------------------------------------+
* | [--->] IV_NUMBER_B                    TYPE        I
* +--------------------------------------------------------------------------------------</SIGNATURE>
  method SET_NUMBER_B.
    MV_NUMBER_B = IV_NUMBER_B.
  endmethod.
ENDCLASS.
```

## YCL_OPERATION_ADD 具体产品类a
```
class YCL_OPERATION_ADD definition
  public
  inheriting from YCL_OPERATION
  final
  create public .

public section.

  methods GET_RESULT
    redefinition .
protected section.
private section.
ENDCLASS.



CLASS YCL_OPERATION_ADD IMPLEMENTATION.


* <SIGNATURE>---------------------------------------------------------------------------------------+
* | Instance Public Method YCL_OPERATION_ADD->GET_RESULT
* +-------------------------------------------------------------------------------------------------+
* | [<-()] RV_RESULT                      TYPE        I
* +--------------------------------------------------------------------------------------</SIGNATURE>
  method GET_RESULT.
    RV_RESULT = MV_NUMBER_A + MV_NUMBER_B.
  endmethod.
ENDCLASS.
```

## YCL_OPERATION_SUB 具体产品类b
```
class YCL_OPERATION_SUB definition
  public
  inheriting from YCL_OPERATION
  final
  create public .

public section.

  methods GET_RESULT
    redefinition .
protected section.
private section.
ENDCLASS.



CLASS YCL_OPERATION_SUB IMPLEMENTATION.


* <SIGNATURE>---------------------------------------------------------------------------------------+
* | Instance Public Method YCL_OPERATION_SUB->GET_RESULT
* +-------------------------------------------------------------------------------------------------+
* | [<-()] RV_RESULT                      TYPE        I
* +--------------------------------------------------------------------------------------</SIGNATURE>
  method GET_RESULT.
    RV_RESULT = MV_NUMBER_A - MV_NUMBER_B.
  endmethod.
ENDCLASS.
```

# 调用程序代码
```
*---2.简单工厂模式
  DATA:
    GO_OPER TYPE REF TO YCL_OPERATION.

  GO_OPER = YCL_OPERATIONFACTORY=>CREATE_INSTANCE( '+' ).

  GO_OPER->SET_NUMBER_A( '3' ).
  GO_OPER->SET_NUMBER_B( '1' ).

  WRITE:/ GO_OPER->GET_RESULT( ).
```